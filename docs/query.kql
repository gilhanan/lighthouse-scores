let _startTime = datetime(2023-09-20);
let _endTime = datetime(2023-12-17);
ClientTelemetry
| where ext_ingest_time between (_startTime .. _endTime)
| where data_action == "Root Handler"
| where data_userAgent == "Mozilla/5.0 (Linux; Android 11; moto g power (2022)) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/109.0.0.0 Mobile Safari/537.36"
| where data_host in ("smp-euap-pb-eus2-staging.azurewebsites.net", "appgallery.spza-canary.net")
| extend url = replace_regex(data_page, @"https:\/\/[^\/]+", "")
| where url in ( 
    "/en-US/product/web-apps/absentify1649267151463.absentify",
    "/embed/en-US/product/power-bi-visuals/WA104380756?product=power-bi-visuals",
    "/en-US/marketplace/apps?product=office"
    )
| extend url = case(
                   url == "/en-US/product/web-apps/absentify1649267151463.absentify",
                   "PDP",
                   url == "/embed/en-US/product/power-bi-visuals/WA104380756?product=power-bi-visuals",
                   "Embed PDP",
                   url == "/en-US/marketplace/apps?product=office",
                   "Gallery",
                   "Other"
               )
| extend data_details = parse_json(data_details)
| extend requestStart = tolong(data_details["requestStart"])
| extend responseStart = tolong(data_details["responseStart"])
| extend responseEnd = tolong(data_details["responseEnd"])
| extend slowestAssetDuration = tolong(data_details["slowestAssetDuration"])
| extend duration = tolong(data_details["duration"])
| order by url, ext_ingest_time asc
| extend rowGroup = ceiling(row_number() / 11)
| summarize 
    responseStart=percentile(responseStart, 50),
    responseEnd=percentile(responseEnd, 50),
    slowestAssetDuration=percentile(slowestAssetDuration, 50),
    duration=percentile(duration, 50)
    by url, rowGroup
| project-away rowGroup